%%%%%%%%%%
% Списки %
%%%%%%%%%%

% Длина списка

count([], 0).
count([_ | T], R) :- count(T, TR), R is TR + 1.
% count/2 соответствует length/2 из стандартной библиотеки

/*
?- count([], R).
   R / 0
?- count([1, 2, 3], R).
   R / 3
*/


% Проверка принадлежности элемента списку

contains(V, [V | _]).
contains(V, [_ | T]) :- contains(V, T).
% contains/2 соответствует memeber/2 из стандартной библиотеки

/*
?- contains(1, [1, 2, 3]).
   yes
?- contains(3, [1, 2, 3]).
   yes
?- contains(10, [1, 2, 3]).
   no
?- contains(10, [R]).
   R / 10
?- contains(10, [1, R, 2]).
   R / 10
?- contains(10, [10, R, 2]).
   yes
   R / 10
?- contains(10, [R1, R2, 2]).
   R1 / 10
   R2 / 10
*/


% Конкатенация списков

concat([], B, B).
concat([H | T], B, [H | R]) :- concat(T, B, R).
% concat/3 соответствует append/3 из стандартной библиотеки

/*
?- concat([], [1, 2, 3], R).
   R / [1,2,3]
?- concat([1, 2, 3], [], R).
   R / [1,2,3]
?- concat([1, 2, 3], [4, 5, 6], R).
   R / [1,2,3,4,5,6]
?- concat([1, 2, 3], R, [1, 2, 3, 4, 5, 6]).
   R / [4,5,6]
?- concat(R, [4, 5, 6], [1, 2, 3, 4, 5, 6]).
   R / [1,2,3]
?- concat([1 | R], [4, 5, 6], [1, 2, 3, 4, 5, 6]).
   R / [2,3]
?- concat([2 | R], [4, 5, 6], [1, 2, 3, 4, 5, 6]).
   no
*/


% Список целых чисел в диапазоне [N, L)

range(L, L, []).
range(N, L, [N | T]) :- N < L, N1 is N + 1, range(N1, L, T).

/*
?- range(10, 10, R).
   R / []
?- range(0, 10, R).
   R / [0,1,2,3,4,5,6,7,8,9]
*/


% Поиск в "ассоциативном массиве"

lookup(K, [(K, V) | _], V).
lookup(K, [_ | T], R) :- lookup(K, T, R).

/*
?- lookup(x, [(x, 10), (y, 20)], R).
   R / 10
?- lookup(y, [(x, 10), (y, 20)], R).
   R / 20
?- lookup(x, [(x, 10), (y, 20), (x, 30)], R).
   R / 10
   R / 30
?- lookup(y, [(x, 10), (y, 20), (x, 30)], R).
   R / 20
?- lookup(z, [(x, 10), (y, 20), (x, 30)], R).
   no
*/


% Zip списков

zip([], _, []) :- !.
zip(_, [], []) :- !.
zip([H1 | T1], [H2 | T2], [(H1, H2) | RT]) :- zip(T1, T2, RT).

/*
?- zip([1, 2, 3], [a, b, c], R).
   R / [(1,a),(2,b),(3,c)]
*/
